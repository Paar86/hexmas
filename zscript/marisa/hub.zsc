// hub-related stuff (inventory resets, the lore giving npc, the central artifact pieces)
Class mkSavedInventory : Actor
{
	Inventory InvSel, InvFirst;
	Weapon rw;
	Default
	{
		+NOGRAVITY;
		+NOBLOCKMAP;
		+DONTSPLASH;
	}
}

Class mkInventoryResetHandler : EventHandler
{
	private void RetrieveInventory( mkSavedInventory saved, PlayerPawn giveto )
	{
		Inventory i, prev, next, top;
		prev = null;
		i = saved.inv;
		top = giveto.inv;
		if ( top )
		{
			while ( top.inv )
			{
				top = top.inv;
			}
		}
		while ( i )
		{
			next = i.inv;
			if ( !i.bUNDROPPABLE )
			{
				if ( top )
				{
					top.inv = i;
					top = i;
				}
				else giveto.inv = top = i;
				i.owner = giveto;
				i.inv = null;
				if ( saved.inv == i ) saved.inv = next;
				if ( prev ) prev.inv = next;
			}
			else prev = i;
			i = next;
		}
		giveto.invsel = saved.invsel;
		giveto.invfirst = saved.invfirst;
		if ( saved.rw )
		{
			giveto.player.PendingWeapon = giveto.player.ReadyWeapon = saved.rw;
			giveto.BringUpWeapon();
		}
		else
		{
			giveto.player.ReadyWeapon = null;
			giveto.player.PendingWeapon = WP_NOCHANGE;
		}
	}

	private void StashInventory( mkSavedInventory stored, PlayerPawn takefrom )
	{
		Inventory i, prev, next, top;
		prev = null;
		i = takefrom.inv;
		top = stored.inv;
		if ( top )
		{
			while ( top.inv )
			{
				top = top.inv;
			}
		}
		while ( i )
		{
			next = i.inv;
			if ( !i.bUNDROPPABLE )
			{
				if ( top )
				{
					top.inv = i;
					top = i;
				}
				else stored.inv = top = i;
				i.owner = stored;
				i.inv = null;
				if ( takefrom.inv == i ) takefrom.inv = next;
				if ( prev ) prev.inv = next;
			}
			else prev = i;
			i = next;
		}
		stored.invsel = takefrom.invsel;
		stored.invfirst = takefrom.invfirst;
		if ( takefrom.invsel && (takefrom.invsel.owner != takefrom) ) takefrom.invsel = null;
		if ( takefrom.invfirst && (takefrom.invfirst.owner != takefrom) ) takefrom.invfirst = null;
		stored.rw = takefrom.player.ReadyWeapon;
		takefrom.player.ReadyWeapon = null;
		takefrom.player.PendingWeapon = WP_NOCHANGE;
	}

	private mkSavedInventory FindSavedInventory( int i )
	{
		let t = ThinkerIterator.Create("mkSavedInventory");
		mkSavedInventory a;
		while ( a = mkSavedInventory(t.Next()) )
		{
			if ( a.special1 == i ) return a;
		}
		return null;
	}

	override void WorldUnloaded( WorldEvent e )
	{
		if ( e.IsSaveGame ) return;
		// stash away current inventory
		for ( int i=0; i<MAXPLAYERS; i++ )
		{
			if ( !playeringame[i] || !players[i].mo ) continue;
			mkSavedInventory stored = FindSavedInventory(i);
			if ( !stored )
			{
				stored = mkSavedInventory(players[i].mo.Spawn("mkSavedInventory"));
				stored.special1 = i;
			}
			StashInventory(stored,players[i].mo);
		}
	}

	override void WorldLoaded( WorldEvent e )
	{
		if ( e.IsSaveGame ) return;
		// clear previous inventory then restore saved one (if any)
		for ( int j=0; j<MAXPLAYERS; j++ )
		{
			if ( !playeringame[j] || !players[j].mo ) continue;
			players[j].mo.ClearInventory();
			mkSavedInventory saved = FindSavedInventory(j);
			if ( saved ) RetrieveInventory(saved,players[j].mo);
			else
			{
				players[j].mo.GiveDefaultInventory();
				// fixup, remove repeated copies of BasicArmor/HexenArmor from inventory
				Inventory i, prev, next, ba, ha;
				prev = ba = ha = null;
				i = players[j].mo.inv;
				while ( i )
				{
					next = i.inv;
					if ( i is 'BasicArmor' )
					{
						if ( !ba ) ba = i;
						else
						{
							if ( players[j].mo.inv == i ) players[j].mo.inv = next;
							if ( prev ) prev.inv = next;
							else prev = next;
							i.Destroy();
						}
					}
					else if ( i is 'HexenArmor' )
					{
						if ( !ha ) ha = i;
						else
						{
							if ( players[j].mo.inv == i ) players[j].mo.inv = next;
							if ( prev ) prev.inv = next;
							else prev = next;
							i.Destroy();
						}
					}
					i = next;
				}
				// set invsel/invfirst to first found selectable item
				for ( Inventory i=players[j].mo.inv; i; i=i.inv )
				{
					if ( !i.bINVBAR ) continue;
					players[j].mo.InvSel = players[j].mo.InvFirst = i;
					break;
				}
			}
			if ( players[j].PendingWeapon != WP_NOCHANGE )
				players[j].mo.BringUpWeapon();
		}
	}
}

// for dialogue advance
Class mkHasClearedHub : Inventory
{
	Default
	{
		+INVENTORY.UNDROPPABLE;
		+INVENTORY.UNTOSSABLE;
	}
}

Class mkSageHealing : Inventory
{
	override bool TryPickup( in out Actor toucher )
	{
		if ( !toucher.GiveBody(-100) )
			return false;
		GoAwayAndDie();
		return true;
	}
}

// the sage of winter, stands still but will walk around if he has a goal
Class mkSageNPC : Actor
{
	int scnt;

	override void Activate( Actor activator )
	{
		Super.Activate(activator);
		bSOLID = true;
		bINCOMBAT = false;
		bINVISIBLE = false;
		SetStateLabel("Spawn");
	}
	override void Deactivate( Actor activator )
	{
		Super.Deactivate(activator);
		bSOLID = false;
		bINCOMBAT = true;	// prevents dialogue
		bINVISIBLE = true;
		SetStateLabel("Inactive");
	}

	Default
	{
		//$Title Sage NPC
		//$Category Marisa/Decoration
		//$Sprite SAGEB1
		Speed 2;
		Radius 16;
		Height 64;
		MONSTER;
		+NEVERTARGET;
		+FRIENDLY;
		+NODAMAGE;
		-COUNTKILL;
		-SHOOTABLE;
	}
	States
	{
	Spawn:
		SAGE B 2;
		SAGE B 2 A_JumpIf(goal,"See");
		Loop;
	See:
		SAGE # 2
		{
			if ( goal )
			{
				if ( (scnt%12) < 6 ) frame = 1;
				else if ( (scnt%24) < 12 ) frame = 0;
				else frame = 2;
				scnt++;
				A_Chase(null,null);
			}
			return A_JumpIf(!goal,"Spawn");
		}
		Wait;
	Greetings:
		SAGE B 1;
		Goto Spawn;
	Inactive:
		TNT1 A -1;
		Stop;
	}
}

Class mkHubGem : Inventory abstract
{
	String GemMsg;

	Property GemMessage : GemMsg;

	Default
	{
		+INVENTORY.UNDROPPABLE;
		+INVENTORY.UNTOSSABLE;
		Inventory.MaxAmount 1;
		Inventory.PickupMessage "You got the %s.\n%s";
	}
	override String PickupMessage()
	{
		return String.Format(PickupMsg,GetTag(),GemMsg);
	}
}

Class mkHubGemFire : mkHubGem
{
	Default
	{
		//$Title Fire Gem
		//$Category Marisa/Key Items
		Inventory.Icon "GEMICN0";
		Tag "Fire Gem";
		mkHubGem.GemMessage "It's warm to the touch.";
	}
	States
	{
	Spawn:
		EGEM A -1;
		Stop;
	}
}
Class mkHubGemWater : mkHubGem
{
	Default
	{
		//$Title Water Gem
		//$Category Marisa/Key Items
		Inventory.Icon "GEMICN1";
		Tag "Water Gem";
		mkHubGem.GemMessage "Although solid, your hands get wet when touching it.";
	}
	States
	{
	Spawn:
		EGEM B -1;
		Stop;
	}
}
Class mkHubGemEarth : mkHubGem
{
	Default
	{
		//$Title Earth Gem
		//$Category Marisa/Key Items
		Inventory.Icon "GEMICN2";
		Tag "Earth Gem";
		mkHubGem.GemMessage "Feels rough to the touch.";
	}
	States
	{
	Spawn:
		EGEM C -1;
		Stop;
	}
}
Class mkHubGemAir : mkHubGem
{
	Default
	{
		//$Title Air Gem
		//$Category Marisa/Key Items
		Inventory.Icon "GEMICN3";
		Tag "Air Gem";
		mkHubGem.GemMessage "Cool air seems to come out of it.";
	}
	States
	{
	Spawn:
		EGEM D -1;
		Stop;
	}
}
/*Class mkHubGemAether : mkHubGem
{
	Default
	{
		//$Title Aether Gem
		//$Category Marisa/Key Items
		Inventory.Icon "GEMICN4";
		Tag "Aether Gem";
		mkHubGem.GemMessage "You can feel some arcane power within.";
	}
	States
	{
	Spawn:
		EGEM E -1;
		Stop;
	}
}*/
Class mkHubGemNether : mkHubGem
{
	Default
	{
		//$Title Nether Gem
		//$Category Marisa/Key Items
		Inventory.Icon "GEMICN5";
		Tag "Nether Gem";
		mkHubGem.GemMessage "There's something off about this gem...";
	}
	States
	{
	Spawn:
		EGEM F -1;
		Stop;
	}
}

/*
   for destruction cutscene
   shamelessly recycled from dummied out 20 heretics content
*/
Class mkNetherGem : Actor
{
	Default
	{
		Radius 6;
		Height 7;
	}
	States
	{
	Spawn:
		EGEM Z -1;
		Stop;
	Raise:
		EGEM Z 0
		{
			bNoGravity = true;
			vel.z = 0.75;
			A_PlaySound("ngem/float",CHAN_6,1.0,true);
			tracer = Spawn("mkDispelLight",Vec3Offset(0,0,7));
			tracer.target = self;
		}
		EGEM Z 3 Bright
		{
			vel.z *= 0.995;
			for ( int i=0; i<16; i++ )
			{
				A_SpawnParticle("20 A0 10",SPF_FULLBRIGHT|SPF_RELPOS,30,FRandom[NGem](1,5),FRandom[NGem](0,360),FRandom[NGem](0,8),0,FRandom[NGem](0,12),accelz:0.1);
				A_SpawnParticle("A0 20 A0",SPF_FULLBRIGHT|SPF_RELPOS,60,FRandom[NGem](3,6),FRandom[NGem](0,360),FRandom[NGem](0,8),0,FRandom[NGem](0,12),accelz:0.1);
			}
			special1++;
			return A_JumpIf(special1>=330,"Death");
		}
		Wait;
	Death:
		TNT1 A 0
		{
			tracer.target = null;
			A_StopSound(CHAN_6);
			Spawn("mkDispelExplo",pos);
			Spawn("mkDispelRing",pos);
		}
		TNT1 AAAAA 1
		{
			for ( int i=0; i<64; i++ )
			{
				A_SpawnParticle("20 A0 10",SPF_FULLBRIGHT,Random[NGem](100,150),FRandom[NGem](2,4),zoff:7,velx:FRandom[NGem](-4,4),vely:FRandom[NGem](-4,4),velz:FRandom[NGem](-4,4),accelz:0.02);
				A_SpawnParticle("A0 20 A0",SPF_FULLBRIGHT,Random[NGem](200,300),FRandom[NGem](4,16),zoff:7,velx:FRandom[NGem](-4,4),vely:FRandom[NGem](-4,4),velz:FRandom[NGem](-4,4),accelz:0.02);
			}
		}
		TNT1 AAAAA 3
		{
			for ( int i=0; i<64; i++ )
			{
				A_SpawnParticle("20 A0 10",SPF_FULLBRIGHT,Random[NGem](300,400),FRandom[NGem](2,4),zoff:7,velx:FRandom[NGem](-2,2),vely:FRandom[NGem](-2,2),velz:FRandom[NGem](-2,2),accelz:0.01);
				A_SpawnParticle("A0 20 A0",SPF_FULLBRIGHT,Random[NGem](600,800),FRandom[NGem](4,16),zoff:7,velx:FRandom[NGem](-2,2),vely:FRandom[NGem](-2,2),velz:FRandom[NGem](-2,2),accelz:0.01);
			}
		}
		Stop;
	}
}

Class mkDispelLight : PointLightAttenuated
{
	int cnt;

	Default
	{
		args 255, 32, 255, 600;
	}
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		cnt = 300;
	}
	override void Tick()
	{
		Super.Tick();
		if ( target ) SetOrigin(target.Vec3Offset(0,0,7),true);
		else
		{
			args[3] = 1200;
			args[0] = int(255*(cnt/300.));
			args[1] = 0;
			args[2] = int(255*(cnt/300.));
			cnt--;
			if ( cnt <= 0 ) Destroy();
		}
	}
}

Class mkDispelExplo : Actor
{
	Default
	{
		RenderStyle "Add";
		Scale 4.0;
		Radius 0;
		Height 0.1;
		+NOBLOCKMAP;
		+NOGRAVITY;
		+DONTSPLASH;
		+FORCEXYBILLBOARD;
	}
	States
	{
	Spawn:
		XXP3 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 4 Bright;
		Stop;
	}
}

Class mkDispelRing : Actor
{
	Default
	{
		RenderStyle "Add";
		Scale 6.0;
		Radius 0;
		Height 0.1;
		+NOBLOCKMAP;
		+NOGRAVITY;
		+DONTSPLASH;
		+FORCEXYBILLBOARD;
	}
	States
	{
	Spawn:
		XRG5 ABCDEFGHIJKLMNOPQRSTUVWX 5 Bright;
		Stop;
	}
}

Class mkDispelSpot : Actor
{
	Default
	{
		RenderStyle "Add";
		Scale 1.5;
		Alpha 0.0;
		Radius 0;
		Height 0.1;
		+NOBLOCKMAP;
		+NOGRAVITY;
		+DONTSPLASH;
		+FORCEXYBILLBOARD;
	}
	States
	{
	Spawn:
		XPPT A -1 Bright;
		Stop;
	}
}

Class mkDispelBeam : Actor
{
	Default
	{
		RenderStyle "Add";
		Alpha 0.0;
		Radius 0;
		Height 0.1;
		RenderRadius 800;
		+NOBLOCKMAP;
		+NOGRAVITY;
		+DONTSPLASH;
		+INTERPOLATEANGLES;
	}
	override void Activate( Actor activator )
	{
		// find target
		target = ActorIterator.Create(args[0]).Next();
		if ( !target )
		{
			Destroy();
			return;
		}
		master = Spawn("mkDispelSpot",pos);
		tracer = Spawn("mkDispelSpot",target.Vec3Offset(0,0,4));
		tracer.scale *= 0.5;
		A_FaceTarget(0,0,0,90);
		SetStateLabel("See");
	}
	override void Deactivate( Actor activator )
	{
		SetStateLabel("Death");
	}
	void A_AdjustBeam()
	{
		if ( !target )
		{
			SetStateLabel("Death");
			return;
		}
		master.SetOrigin(pos,true);
		tracer.SetOrigin(target.Vec3Offset(0,0,4),true);
		if ( alpha < 0.999 ) alpha = min(alpha+0.01,0.999);
		master.alpha = alpha;
		tracer.alpha = alpha*0.15;
		A_SetScale(3.,Distance3D(target));
		A_FaceTarget(0,0,0,90,0,4);
		special1++;
		if ( !(special1%3) )
		{
			for ( int i=0; i<16; i++ )
				A_SpawnParticle("A0 20 A0",SPF_FULLBRIGHT|SPF_RELPOS,60,FRandom[NGem](3,6),FRandom[NGem](0,360),FRandom[NGem](0,8),0,FRandom[NGem](0,12),accelz:0.1);
		}
	}
	States
	{
	Spawn:
		TNT1 A -1;
		Stop;
	See:
		PRKM A 1 Bright A_AdjustBeam();
		Wait;
	Death:
		PRKM A 1
		{
			alpha -= 0.02;
			master.alpha = alpha;
			tracer.alpha = alpha*0.15;
			if ( alpha <= 0 )
			{
				tracer.Destroy();
				master.Destroy();
				Destroy();
			}
		}
		Wait;
	}
}

Class mkHubGemDisplayHandler : EventHandler
{
	Array<Class<Inventory> > gems;

	override void WorldLoaded( WorldEvent e )
	{
		gems.Clear();
		for ( int i=0; i<AllActorClasses.Size(); i++ )
		{
			let c = AllActorClasses[i];
			if ( c.GetParentClass() != 'mkHubGem' ) continue;
			gems.Push((Class<Inventory>)(c));
		}
	}

	override void RenderOverlay( RenderEvent e )
	{
		if ( !automapactive ) return;
		int owned = 0;
		for ( int i=0; i<gems.Size(); i++ )
			owned += !!players[consoleplayer].mo.CountInv(gems[i]);
		Vector2 gs = TexMan.GetScaledSize(GetDefaultByType(gems[0]).Icon);
		Vector2 hscale = StatusBar.GetHudScale();
		Vector2 vsize = (Screen.GetWidth()/hscale.x,Screen.GetHeight()/hscale.y);
		double xpos, ypos;
		xpos = vsize.x-(gs.x+4);
		ypos = (StatusBar.GetTopOfStatusBar()/hscale.y-(owned*gs.y+(owned-1)*4))/2.;
		for ( int i=0; i<gems.Size(); i++ )
		{
			if ( !players[consoleplayer].mo.CountInv(gems[i]) ) continue;
			Screen.DrawTexture(GetDefaultByType(gems[i]).Icon,false,xpos,ypos,DTA_VirtualWidthF,vsize.x,DTA_VirtualHeightF,vsize.y,DTA_KeepRatio,true,DTA_TopOffset,0,DTA_LeftOffset,0);
			ypos += (gs.y+4);
		}
	}
}
