// for dialogue advance
Class mkBossClearedToken : Inventory {}

Class mkClericNPC : Actor
{
	Actor spawnspot;

	Default
	{
		//$Title Cleric NPC
		//$Category Marisa/Decoration
		Translation "146:163=[32,0,0]:[255,0,0]";
		Health 100;
		PainChance 255;
		Speed 10;
		Radius 16;
		Height 64;
		MONSTER;
		+FRIENDLY;
		-COUNTKILL;
		Obituary "%o got bludgeoned by a cleric.";
		Tag "Cleric";
	}
	override int DamageMobj( Actor inflictor, Actor source, int damage, Name mod, int flags, double angle )
	{
		if ( (Health > 0) && source && source.player && bFRIENDLY )
		{
			A_PrintBold("Have you gone insane?");
			bFRIENDLY = false;
		}
		return Super.DamageMobj(inflictor,source,damage,mod,flags,angle);
	}
	action void A_MaceAttack()
	{
		if ( !target ) return;
		if ( CheckMeleeRange() )
		{
			int damage = 25+(random[MaceAtk]()&15);
			A_PlaySound("EttinAttack",CHAN_WEAPON);
			int newdam = target.DamageMobj(self,self,damage,'Melee');
			target.TraceBleed(newdam>0?newdam:damage,self);
		}
		else A_PlaySound("FighterHammerMiss",CHAN_WEAPON);
	}
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		spawnspot = Spawn("MapSpot",SpawnPoint);
	}
	States
	{
	Spawn:
		CLER A 2
		{
			A_LookEx(LOF_NOJUMP);
			if ( target && (!bFRIENDLY || !target.player) )
				return ResolveState("See");
			return ResolveState(null);
		}
		Wait;
	See:
		CLER AABBCCDD 2
		{
			A_Chase();
			if ( !target || (target.health <= 0) || (target.player && bFRIENDLY) )
			{
				A_ClearTarget();
				goal = spawnspot;
				if ( !goal ) ThrowAbortException("Spawn Spot does not exist for Cleric NPC");
				return ResolveState("Returning");
			}
			return ResolveState(null);
		}
		Loop;
	Returning:
		CLER AABBCCDD 2
		{
			A_Chase(null,null);
			if ( Distance3D(spawnspot) < speed )
			{
				SetOrigin(SpawnPoint,true);
				angle = SpawnAngle;
				A_ClearTarget();
				A_ResetHealth();
				bFRIENDLY = true;
				goal = null;
				return ResolveState("Spawn");
			}
			return ResolveState(null);
		}
		Loop;
	Melee:
		CLER EF 6 A_FaceTarget();
		CLER G 6 A_MaceAttack();
		Goto See;
	Pain:
		CLER H 4;
		CLER H 4 A_PlaySound("PlayerClericPain");
		Goto See;
	Death:
		CLER I 6;
		CLER J 6 A_PlaySound("PlayerClericNormalDeath");
		CLER KL 6;
		CLER M 6 A_NoBlocking();
		CLER NOP 6;
		CLER Q -1;
		Stop;
	XDeath:
		CLER R 5 A_PlaySound("PlayerClericCrazyDeath");
		CLER S 5;
		CLER T 5 A_NoBlocking();
		CLER UVWXYZ 5;
		CLER [ -1;
		Stop;
	Ice:
		TNT1 A 0 { Translation = 0; }
		CLER \ 5 A_FreezeDeath();
		CLER \ 1 A_FreezeDeathChunks();
		Wait;
	Burn:
		FDTH C 5 Bright A_PlaySound("PlayerClericBurnDeath");
		FDTH D 4 Bright;
		TNT1 A 0 { Translation = 0; }
		FDTH G 5 Bright;
		FDTH H 4 Bright A_PlaySound("PlayerClericCrazyDeath");
		FDTH I 5 Bright;
		FDTH J 4 Bright;
		FDTH K 5 Bright;
		FDTH L 4 Bright;
		FDTH M 5 Bright;
		FDTH N 4 Bright;
		FDTH O 5 Bright;
		FDTH P 4 Bright;
		FDTH Q 5 Bright;
		FDTH R 4 Bright;
		FDTH S 5 Bright A_NoBlocking();
		FDTH T 4 Bright;
		FDTH U 5 Bright;
		FDTH V 4 Bright;
		Stop;
	}
}

Class mkApprenticeSighting : Actor
{
	int scnt, bcnt;
	bool stp;
	override void Activate( Actor activator )
	{
		if ( !InStateSequence(CurState,FindState("Spawn")) ) return;
		let t = ThinkerIterator.Create("MapSpot");
		Actor a;
		while ( a = Actor(t.Next()) )
		{
			if ( a.tid != args[0] ) continue;
			target = a;
			break;
		}
		if ( target ) SetStateLabel("Turn");
	}
	override void Tick()
	{
		Super.Tick();
		if ( --bcnt <= 0 )
		{
			bcnt = 50;
			A_PlaySound("apprentice/active",CHAN_5,0.5,attenuation:4.0);
		}
	}
	Default
	{
		//$Title Elder Apprentice (Sighting)
		//$Category Marisa/Decoration
		Translation "0:255=@100[0,0,0]", "187:202=216:216";
		Speed 8;
		Radius 16;
		Height 64;
		+SOLID;
		+CANPASS;
		+DROPOFF;
		Tag "????";
	}
	States
	{
	Spawn:
		MAGE A -1 Bright;
		Stop;
	Turn:
		MAGE A 40 Bright;
		MAGE A 1 Bright A_FaceTarget(6);
		MAGE A 0 Bright A_JumpIf(deltaangle(angle,AngleTo(target))<double.epsilon,"Run");
		Goto Turn+1;
	Run:
		MAGE AAABBBCCCDDD 2 Bright
		{
			if ( !(scnt%6) )
			{
				if ( stp ) A_PlaySound("world/amb4",CHAN_AUTO,0.5,attenuation:3.5);
				else A_PlaySound("world/amb11",CHAN_AUTO,0.5,attenuation:3.5);
				stp = !stp;
			}
			scnt++;
			if ( Distance2D(target) <= speed ) Destroy();
			else TryMove(Vec2Offset(cos(angle)*speed,sin(angle)*speed),32);
		}
		Loop;
	}
}
